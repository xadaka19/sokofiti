plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new RuntimeException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '29'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '2.6.0'
}


def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    println "✅ key.properties loaded successfully"
    println "   storeFile: ${keystoreProperties['storeFile']}"
    println "   keyAlias: ${keystoreProperties['keyAlias']}"

    // Verify keystore file exists
    def storeFilePath = keystoreProperties['storeFile']
    if (storeFilePath) {
        def keystoreFile = file(storeFilePath)
        if (keystoreFile.exists()) {
            println "✅ Keystore file exists at: ${keystoreFile.absolutePath}"
        } else {
            println "❌ ERROR: Keystore file NOT found at: ${storeFilePath}"
        }
    }
} else {
    println "❌ ERROR: key.properties file not found at: ${keystorePropertiesFile.absolutePath}"
}


android {
    namespace "com.sokofiti.app"
    compileSdkVersion 36
    //ndkVersion "26.3.11579264"
    //ndkVersion "25.1.8937393"
    //buildToolsVersion "30.0.3"

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appName = "eClassify"
            def versionName = flutter.versionName
            // Try to extract ABI from the output file name (dirty but effective)
            def abiSuffix = ""
            if (output.outputFileName.contains("v7a")) {
                abiSuffix = "(v7a)"
            } else if (output.outputFileName.contains("v8a")) {
                abiSuffix = "(v8a)"
            } else if (output.outputFileName.contains("x86_64")) {
                abiSuffix = "(x86_64)"
            }
            outputFileName = "${appName}-${versionName}${abiSuffix}.apk"
        }
    }

    defaultConfig {
        applicationId "com.sokofiti.app"
        minSdkVersion 24
        targetSdkVersion 36
        multiDexEnabled true
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }


    signingConfigs {
        release {
            def storeFilePath = keystoreProperties['storeFile']
            if (storeFilePath == null || storeFilePath.isEmpty()) {
                throw new RuntimeException("storeFile not set in key.properties")
            }
            def keystoreFile = file(storeFilePath)
            if (!keystoreFile.exists()) {
                throw new RuntimeException("Keystore file not found: ${keystoreFile.absolutePath}")
            }
            storeFile keystoreFile
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            println "✅ Release signing config loaded successfully"
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
            // R8 minification disabled - causes OOM even with 16GB RAM
            // The deobfuscation warning in Play Console can be ignored
            minifyEnabled false
            shrinkResources false
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:32.7.2')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.play:integrity:1.4.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.3'
    implementation 'com.google.android.gms:play-services-maps:19.0.0'
    implementation 'com.google.firebase:firebase-messaging:24.0.3'
}


